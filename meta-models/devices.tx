//Device
Device:
    Board | Peripheral
;

// Board meta-model
Board:
    'board:'
	((memory=MEMORY)
	 (cpu=CPU)
	 ("pins" ":" val*=PIN)
	 ("name" ":" name=ID)
	 ("riot_name" ":" riot_name=NEW_ID)
	 ("vcc" ":" vcc=FLOAT)
	 ("operating_voltage" ":" op_volt=FLOAT)
	 (network=NETWORK)?
	 (bluetooth=BLUETOOTH)?)#
;

Peripheral:
    'peripheral:'
	 (("type" ":" type=PER_TYPES)
	 ("pins" ":" val*=PIN)
	 ("name" ":" name=ID)
	 ("riot_name" ":" riot_name=NEW_ID)
	 ("vcc" ":" vcc=FLOAT)
	 ("operating_voltage" ":" op_volt=FLOAT)
	 (network=NETWORK)?
	 (bluetooth=BLUETOOTH)?)#
;

PER_TYPES:
	"sensor" | "actuator"
;

PIN:
	POWER_PIN | IO_PIN | INPUT_PIN | OUTPUT_PIN
;

POWER_PIN:
	"- power:"
	(("name:" name=ID)
	 ("number:" number=INT)
	 ("type:" type=POWER_TYPE))#
;

POWER_TYPE:
	("gnd" | "5v" | "3v3")
;

IO_PIN:
	"- io_pin:" "->" funcs*=PIN_FUNC[","]
	(("name:" name=ID)
	 ("number:" number=INT)
	 ("vmax:" vmax=NUMBER)?)#
;

PIN_FUNC:
	GPIO | I2C | SPI | UART | PWM | ADC | DAC
;

GPIO:
	type="gpio"
;

I2C:
	type=I2C_TYPE "-" bus=INT
;

I2C_TYPE:
	"sda" | "scl"
;

SPI:
	type=SPI_TYPE "-" bus=INT
;

SPI_TYPE:
	"mosi" | "miso" | "sck" | "cs"
;

UART:
	type=UART_TYPE "-" bus=INT
;

UART_TYPE:
	"tx" | "rx"
;

PWM:
	type="pwm-" interface=INT 
;

ADC:
	type="adc"
;

DAC:
	type="dac"
;

INPUT_PIN:
	"- input_pin:"
	(("name:" name=ID)
	 ("number:" number=INT)
	 ("vmax:" vmax=NUMBER)?)#
;

OUTPUT_PIN:
	"- output_pin:"
	(("name:" name=ID)
	 ("number:" number=INT)
	 ("vmax:" vmax=NUMBER)?)#
;

CPU:
	"cpu" ":"
	(("cpu_family:" cpu_family=CPU_FAMILY)
	 ("max_freq:" max_freq=NUMBER unit=FREQ_UNIT)
	 ("fpu:" fpu=BOOL))#
;


CPU_FAMILY:
	( "ESP32" | "ESP8266")
;

MEMORY:
    "memory:"
	(
    (("ram:" ram=NUMBER UNITS?)
    ("rom:" rom=NUMBER UNITS?)?
    ("flash:" flash=NUMBER UNITS?)?)# |
    (("ram:" ram=NUMBER UNITS?)?
    ("rom:" rom=NUMBER UNITS?)?
    ("flash:" flash=NUMBER UNITS?))# |
    (("ram:" ram=NUMBER UNITS?)?
    ("rom:" rom=NUMBER UNITS?)
    ("flash:" flash=NUMBER UNITS?)?)#
    )
;

UNITS:
    ("kb" | "mb" | "gb" | "b")
;

NETWORK:
    "network" ":"
    (WIFI|ETHERNET)
;

WIFI:
	"- wifi:" 
	(("name:" name=ID)
	 ("freq:" freq=NUMBER FREQ_UNIT)?)#
;

FREQ_UNIT:
	"ghz" | "mhz" | "hz"
;

ETHERNET:
	"- ethernet:"
	"name:" name=ID
;

BLUETOOTH:
	"bluetooth" ":" 
	"version:" version=NUMBER
;

Comment:
  /\/\/.*$/
;

NEW_ID:
	/(.*?)\n/
;