// Grammar for a system. A system consists a board (ESP32/ESP8266) 
// and one or more peripherals (sensors/actuators).

SYSTEM:
	includes*=INCLUDE
	connections*=CONNECTION
;

INCLUDE:
	"include" name=ID
;

CONNECTION:
	"connection:"
	(("name:" name=ID)
	 ("board:" board=BOARD)
	 ("peripheral:" peripheral=PERIPHERAL)
	 ("power_connections:"? power_conns*=POWER_CONNECTION)
	 ("hw_connections:" hw_conns+=HW_CONNECTION)
	 ("communication_endpoint:" com_endpoint=COM_ENDPOINT)?)#
;

PERIPHERAL:
	device=ID ("("number=INT")")?
;

BOARD:
	device=ID ("("number=INT")")?
;

POWER_CONNECTION:
	"-" board_power=ID "--" peripheral_power=ID
;

HW_CONNECTION:
	GPIO | I2C | SPI | UART
;

GPIO:
	"-" type="gpio" ":" board_int=ID "--" peripheral_int=ID
;

FREQUENCY:
	val=INT unit=FREQ_UNIT
;

FREQ_UNIT:
	"hz" | "khz" | "mhz" | "ghz"
;

SPI:
	"-" type="spi" ":" 
	"mosi:" board_int=ID "--" peripheral_int=ID
	"miso:" board_int=ID "--" peripheral_int=ID
	"sck:" board_int=ID "--" peripheral_int=ID
	"cs:" board_int=ID "--" peripheral_int=ID
;

I2C:
	"-" type="i2c" ":" 
	"sda:" board_int=ID "--" peripheral_int=ID
	"scl:" board_int=ID "--" peripheral_int=ID
	"slave_address:" "0x" slave_addr=INT
;

UART:
	"-" type="uart" ":"
	"tx:" board_int=ID "--" peripheral_int=ID
	"rx:" board_int=ID "--" peripheral_int=ID
	"baudrate:" baudrate=INT
;

COM_ENDPOINT:
	(("topic:" topic=NEW_ID)
	 ("wifi_ssid:" wifi_ssid=NEW_ID)
	 ("wifi_passwd:" wifi_passwd=NEW_ID)
	 ("address:" addr=ADDRESS_ID)
	 ("port:" port=INT)
	 ("msg:" msg=MSG_ENTRIES)
     ("frequency:" freq=FREQUENCY)?)#
;

NEW_ID:
	/(.*?)\n/
;

ADDRESS_ID:
	/([0-9a-fA-F]{1,4}:){7,7}[0-9a-fA-F]{1,4}/
;

MSG_ENTRIES:
	msg_entries*=MSG_TYPES[","]
;

MSG_TYPES:
	SENSOR | ACTUATOR
;

SENSOR:
	"Distance" | "Temperature" | "Humidity" | "Gas" | "Pressure" | "Env" | "Acceleration"
;

ACTUATOR:
	"Motor_Controller" | "Leds_Controller" | "Servo_Controller"
;

Comment:
  /\/\/.*$/
;
