// Grammar for a system. A system consists a board (ESP32/ESP8266) 
// and one or more peripherals (sensors/actuators).

SYSTEM:
	includes*=INCLUDE
	connections*=CONNECTION
;

INCLUDE:
	"include" name=ID
;

INCLUDE:
   "include" val=INCLUDE_TYPES
;

INCLUDE_TYPES:
	"esp32_wroom_32" | "wemos_d1_mini" | "srf04" | "bme680"
;

CONNECTION:
	"connection:"
	(("name:" name=ID)
	 ("board:" board=BOARD)
	 ("peripheral:" peripheral=PERIPHERAL)
	 ("power_connections:"? power_conns*=POWER_CONNECTION)
	 ("hw_connections:" hw_conns+=HW_CONNECTION)
	 ("communication_endpoint:" com_endpoint=COM_ENDPOINT)?)#
;

PERIPHERAL:
	device=ID ("("number=INT")")?
;

BOARD:
	device=ID ("("number=INT")")?
;

POWER_CONNECTION:
	"-" board_power=ID "--" peripheral_power=ID
;

HW_CONNECTION:
	GPIO | I2C | SPI | PWM | UART
;

GPIO:
	"-" type="gpio" ":" board_int=ID "--" peripheral_int=ID
;

PWM:
	"-" type="pwm" ":" board_int=ID "--" peripheral_int=ID
	"frequency:" frequency=FREQUENCY
;

FREQUENCY:
	val=INT unit=FREQ_UNIT
;

FREQ_UNIT:
	"hz" | "khz" | "mhz" | "ghz"
;

SPI:
	"-" type="spi" ":" board_int=ID "--" peripheral_int=ID
;

I2C:
	"-" type="i2c" ":" 
	"sda:" board_int=ID "--" peripheral_int=ID
	"scl:" board_int=ID "--" peripheral_int=ID
	"slave_address:" "0x" slave_addr=INT
;

UART:
	"-" type="uart" ":" board_int=ID "--" peripheral_int=ID
	"baudrate:" baudrate=INT
;

COM_ENDPOINT:
	(("topic:" topic=NEW_ID)
	 ("address:" addr=ADDRESS_ID)
	 ("port:" port=INT)
	 ("msg:" msg=MSG_ENTRIES)
     ("frequency:" freq=FREQUENCY)?)#
;

NEW_ID:
	/(.*?)\n/
;

ADDRESS_ID:
	/([0-9a-fA-F]{1,4}:){7,7}[0-9a-fA-F]{1,4}/
;

MSG_ENTRIES:
	msg_entries*=MSG_TYPES[","]
;

MSG_TYPES:
	SENSOR | ACTUATOR
;

SENSOR:
	"Distance" | "Temperature" | "Humidity" | "Gas" | "Pressure" | "Env"
;

ACTUATOR:
	"Motor_Controller" | "Leds_Controller" | "Servo_Controller"
;

Comment:
  /\/\/.*$/
;
